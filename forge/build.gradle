plugins {
    id 'me.shedaniel.unified-publishing'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").file("src/main/resources/gag.accesswidener")
    forge {
        convertAccessWideners = true
        extraAccessWideners.add("gag.accesswidener")
        mixinConfigs "gag.mixins.json"
    }
}

dependencies {
    forge("net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}")

    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    modApi("dev.ftb.mods:ftb-library-forge:${ftb_lib_version}")

    modLocalRuntime modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    bundle(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

processResources {
    def toReplace = [
            "version"             : project.version,
            "ftb_lib_version"     : rootProject.ftb_lib_version,
            "architectury_version": rootProject.architectury_version
    ]

    inputs.properties toReplace
    filesMatching("META-INF/mods.toml") {
        expand toReplace
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

unifiedPublishing {
    project {
        releaseType = rootProject.artifact_type
        gameVersions = ["1.18.2"]
        gameLoaders = ["forge"]
        displayName = "$mod_name Forge $project.version"
        changelog = getGitChangelog()
        mainPublication remapJar

        relations {
            depends {
                curseforge = "architectury-api"
            }
            depends {
                curseforge = "ftb-library"
            }
        }

        if (ENV.CURSE_API_KEY) {
            curseforge {
                token = ENV.CURSE_API_KEY
                id = project.curseforge_id
            }
        }

        // can't use modrinth yet because of FTB Lib (TODO?: migrate to another config lib)
        /*if (ENV.MODRINTH_TOKEN) {
            modrinth {
                token = ENV.MODRINTH_TOKEN
                id = project.modrinth_id
                version = "$project.version+$project.name"
            }
        }*/
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = "${rootProject.archives_base_name}-${project.name}"
            from components.java
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "max"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}
